
class Foo {
    static def foo() = 'Foo#foo return value'
}

class Bar {
    static def __call() = new()

    static def bar() = 'Bar#bar ---'
}

class Baz(Foo) {
    new(foo, bar) = new(foo=foo, bar=bar)

    def add_one_to_foo(self) = self.foo + 1
}

class NoExplicitConstructor(Foo)

def __test_static_method_call() {
    assert_eq(Foo.foo(), 'Foo#foo return value')
    assert_eq(Bar.bar(), 'Bar#bar ---')

    """
    When an invalid attribute is retrieved, we should throw
    """
    assert_throws(def = Foo.not_a_valid_attr)

    """
    Static methods are not inherited
    """
    assert_throws(def = Baz.foo())
}

def __test_class_with_call() {
    r###"
    Test custom static '__call' methods
    "###
    class ClassWithCall {
        static def __call(x) = new(a = x, b = x + x)
    }
    cwc = ClassWithCall(5)
    assert(type(cwc) is ClassWithCall)
    assert_eq(type(cwc), ClassWithCall)
    assert(type(cwc) != Baz)
    assert_eq(cwc.a, 5)
    assert_eq(cwc.b, 10)

    class TraitWithCall {
        static def __call() = 12345
    }

    assert_eq(TraitWithCall(), 12345)

    class TraitWithoutCall {}

    assert_throws(def = TraitWithoutCall())
}

def __test_instantiation() {
    baz = Baz('foo value 123', 565656)
    assert_eq(baz.foo, 'foo value 123')
    assert_eq(baz.bar, 565656)

    baz = Baz(55.555, foo='---')
    assert_eq(baz.foo, '---')
    assert_eq(baz.bar, 55.555)

    baz = Baz(foo=150, bar='j')
    assert_eq(baz.add_one_to_foo(), 151)

    assert_throws(def = NoExplicitConstructor())
}

def __test_str() {
    """
    Make sure that classes display their full names when being printed.
    Full names of builtin classes are actually same as their short names
    """
    assert_eq(str(Baz), '<class lang.tests.cls.a#Baz>')
    assert_eq(str(String), '<class String>')
}
